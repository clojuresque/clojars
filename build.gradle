group = 'clojuresque'
version = '1.5.0-SNAPSHOT'

ext.isReleaseVersion = !version.endsWith("SNAPSHOT")

apply {
    plugin 'signing'
    plugin 'maven'
    plugin 'groovy'
}

repositories {
    mavenCentral()
    maven { url "https://clojars.org/repo" }
}

configurations {
    deployerJars
}

dependencies {
    compile gradleApi()
    compile localGroovy()

    compile 'clojuresque:clojuresque-common:1.1.0'

    testCompile 'org.spockframework:spock-core:0.7-groovy-1.8'

    deployerJars 'org.apache.maven.wagon:wagon-http-lightweight:2.2'
}

jar {
    into("META-INF/clojuresque/clojuresque-clojars") {
        from(project.projectDir) {
            include "LICENSE"
            include "EUPL-v1.1"
            include "MIT"
        }
    }
}

uploadArchives {
    repositories {
        mavenDeployer {
            configuration = configurations.deployerJars
            repository(url: "https://clojars.org/repo") {
                authentication(
                    userName: project.properties["clojuresque.clojars.username"],
                    password: project.properties["clojuresque.clojars.password"]
                )
            }
            pom.project {
                licenses {
                    license {
                        name 'MIT License'
                        url 'http://opensource.org/licenses/MIT'
                        distribution 'repo'
                    }
                }
                description "A Clojars plugin for the Gradle build system"
                url "http://bitbucket.org/clojuresque/clojars"
                scm {
                    connection 'scm:hg:hg@bitbucket.org:clojuresque/clojars'
                    developerConnection 'scm:hg:hg@bitbucket.org:clojuresque/clojars'
                    url 'hg@bitbucket.org:clojuresque/clojars'
                }
            }
            beforeDeployment { signing.signPom(it) }
        }
    }
}

signing {
    sign configurations.archives
}

task wrapper(type: Wrapper) {
    gradleVersion = "1.10"
}
